plugins {
    id 'org.springframework.boot' version '1.5.1.RELEASE'
}

ext {
    //模块名
    moduleName = "springbootDemo";
    version "${parent.version}"
}

configurations {
    querydslapt
}

sourceSets {
    generated {
        java {
            srcDirs = ['src/main/generated']
        }
    }
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
    dependsOn generateQueryDSL
    source generateQueryDSL.destinationDir
}

compileGeneratedJava {
    dependsOn generateQueryDSL
    options.warnings = false
    classpath += sourceSets.main.runtimeClasspath
}

clean {
    delete sourceSets.generated.java.srcDirs
}

idea {
    module {
        sourceDirs += file('src/main/generated')
    }
}

dependencies {
    compile(
            lib."spring-boot-starter-web",
            lib."spring-boot-starter-data-jpa",
            lib."mysql-connector-java",
            lib."querydsl-jpa",
            lib."querydsl-apt",
    )

    testCompile(
            lib."spring-boot-starter-test",
    )
}